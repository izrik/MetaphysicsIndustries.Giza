
/*****************************************************************************
 *                                                                           *
 *  Supergrammar.txt                                                         *
 *  3 May 2010                                                               *
 *  Project: MetaphysicsIndustries.Giza                                      *
 *  Written by: Richard Sartor                                               *
 *  Copyright © 2010-2013 Metaphysics Industries, Inc.                       *
 *                                                                           *
 *  The supergrammar - this grammar defines the language used to write       *
 *    other grammars.                                                        *
 *                                                                           *
 *****************************************************************************/

grammar = ( definition | comment )+;

definition = directive* identifier '=' expr ';';

directive = '<' directive-item ( ',' directive-item )* '>';
directive-item = ( 
    id-include id-whitespace | 
    id-ignore id-case | 
    id-atomic
);
<include whitespace, ignore case> id-include = 'include';
<include whitespace, ignore case> id-whitespace = 'whitespace';
<include whitespace, ignore case> id-ignore = 'ignore';
<include whitespace, ignore case> id-case = 'case';
<include whitespace, ignore case> id-atomic = 'atomic';

expr = ( subexpr | orexpr | comment )+;

orexpr = '(' expr ( '|' expr )* ')' modifier?;

subexpr = ( identifier | literal | charclass ) modifier? ( ':' identifier:tag )?;

modifier = [*+?];

<include whitespace, atomic> number = [\d]+;

<include whitespace, atomic> identifier = [\w-_] [\w-_\d]*;

<include whitespace> literal = '\'' ( [^\\'] | '\\' [rnt\\'] | unicodechar )+ '\'';

<include whitespace> charclass = '[' ( [^\\\[\]] | '\\' [wldsrnt\\\[\]] | unicodechar )+ ']';

<include whitespace, ignore case> unicodechar = '\\x' [\dabcdef][\dabcdef][\dabcdef][\dabcdef];

<include whitespace> comment = ( '/*' ( [^*] | '*'+ [^*/] )* '*'+ '/' | '//' [^\r\n]* [\r\n]+ );

