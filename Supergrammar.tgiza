
/*****************************************************************************
 *                                                                           *
 *  Supergrammar.tgiza                                                       *
 *  15 July 2013                                                             *
 *  Project: MetaphysicsIndustries.Giza                                      *
 *  Written by: Richard Sartor                                               *
 *  Copyright © 2010-2013 Metaphysics Industries, Inc.                       *
 *                                                                           *
 *  The supergrammar - this grammar defines the language used to write       *
 *    other grammars.                                                        *
 *                                                                           *
 *****************************************************************************/

grammar = definition+;

definition = directive* identifier '=' expr ';';

directive = '<' directive-item ( ',' directive-item )* '>';
directive-item = (
    id-mind id-whitespace |
    id-ignore id-case |
    id-atomic |
    id-token |
    id-subtoken |
    id-comment
);
<ignore case, token> id-mind = 'mind';
<ignore case, token> id-whitespace = 'whitespace';
<ignore case, token> id-ignore = 'ignore';
<ignore case, token> id-case = 'case';
<ignore case, token> id-atomic = 'atomic';
<ignore case, token> id-token = 'token';
<ignore case, token> id-subtoken = 'subtoken';
<ignore case, token> id-comment = 'comment';

expr = ( subexpr | orexpr )+;

orexpr = '(' expr ( '|' expr )* ')' modifier?;

subexpr = ( identifier | literal | charclass ) modifier? ( ':' identifier:tag )?;

<token> modifier = [*+?];

<token> number = [\d]+;

<token> identifier = [\w-_] [\w-_\d]*;

<token> literal = '\'' ( [^\\'] | '\\' [rnt\\'] | unicodechar )+ '\'';

<token> charclass = '[' ( [^\\\[\]] | '\\' [wldsrnt\\\[\]] | unicodechar )+ ']';

<ignore case, subtoken> unicodechar = '\\x' [\dabcdef][\dabcdef][\dabcdef][\dabcdef];

<comment> comment = ( '/*' ( [^*] | '*'+ [^*/] )* '*'+ '/' | '//' [^\n]* '\n' );

